buildscript {
    ext {
        // App version
        versionName = '1.9.0'
        versionCode = 1

        // SDK and tools
        compileVersion = 28
        minVersion = 21
        targetVersion = 28
        ndkVersion = '21.3.6528147'

        kotlinVersion = '1.7.10'

        ktlintPinterestVersion = '0.47.0'

        supportVersion = '1.0.0'
        constraintVersion = '1.1.3'

        rxJavaVersion = '2.2.7'
        coroutinesVersion = '1.4.2'

        gsonVersion = '2.8.2'

        zXingVersion = '3.5.0'

        spoungyCastleVersion = '1.58.0.0'
        ed25519Version = '0.3.0'
        bip39version = '2019.01.27'
        web3jVersion = '4.8.0'
        apacheXercesVersion = '2.9.0'

        junitVersion = '4.12'
        mockitoVersion = '3.12.4'
        wsVersion = '2.10'
        xxHashVersion = '1.7.1'
        svgVersion = '1.4'

        androidDep = "androidx.appcompat:appcompat:$supportVersion"
        constraintDep = "androidx.constraintlayout:constraintlayout:$constraintVersion"
        materialDep = "com.google.android.material:material:$supportVersion"

        rxJavaDep = "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
        coroutinesDep = "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

        gsonDep = "com.google.code.gson:gson:$gsonVersion"

        bouncyCastleDep = 'org.bouncycastle:bcprov-jdk15on:1.65'

        ed25519Dep = "net.i2p.crypto:eddsa:$ed25519Version"
        web3jDep = "org.web3j:crypto:$web3jVersion"
        apacheXercesDep = "org.eclipse.birt.runtime.3_7_1:org.apache.xerces:$apacheXercesVersion"

        bip39Dep = "io.github.novacrypto:BIP39:$bip39version"
        secureStringDep = "io.github.novacrypto:securestring:$bip39version@jar"
        wsDep = "com.neovisionaries:nv-websocket-client:$wsVersion"
        xxHashDep = "org.lz4:lz4-java:$xxHashVersion"
        svgDep = "com.caverock:androidsvg-aar:$svgVersion"

        jUnitDep = "junit:junit:$junitVersion"
        mockitoDep = "org.mockito:mockito-inline:$mockitoVersion"

        androidTestRunnerDep = 'androidx.test:runner:1.4.0'
        androidTestRulesDep = 'androidx.test:rules:1.4.0'
        androidJunitDep = 'androidx.test.ext:junit:1.1.3'

        coroutinesTestDep = "org.jetbrains.kotlinx:kotlinx-coroutines-test:${coroutinesVersion}"
    }
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath 'org.mozilla.rust-android-gradle:plugin:0.9.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
    }
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'


allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://jitpack.io"
        }
        mavenCentral()
    }
}

configurations {
    ktlint
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencies {
    ktlint("com.pinterest:ktlint:$ktlintPinterestVersion") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--reporter=plain"
    args "--android"
    args "--reporter=checkstyle,output=${project.buildDir}/reports/checkstyle/ktlint.xml"
    args "$project.rootDir/**/src/main/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "$project.rootDir/**/src/main/**/*.kt"
    args "--android"
}
apply from: "${rootDir}/scripts/publish-root.gradle"
