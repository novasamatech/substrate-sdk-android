apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.mozilla.rust-android-gradle.rust-android'
apply from: '../maven-publish-helper.gradle'

publishing {
    publications {
        release(MavenPublication) {
            groupId 'io.github.nova-wallet'
        }
    }
}

android {
    compileSdkVersion rootProject.compileVersion

    defaultConfig {
        minSdkVersion rootProject.minVersion
        targetSdkVersion rootProject.targetVersion
        versionCode rootProject.versionCode
        versionName computeVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared/java"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/testShared/java"
        }
    }
    kotlinOptions {
        freeCompilerArgs = ['-Xinline-classes']
    }
}

cargo {
    module = "../sr25519-java/"
    libname = "sr25519java"
    targets = ["arm", "arm64", "x86", "x86_64"]
}

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation bouncyCastleDep
    implementation ed25519Dep
    implementation svgDep
    implementation wsDep
    implementation secureStringDep
    implementation gsonDep
    implementation web3jDep
    implementation apacheXercesDep
    implementation xxHashDep

    implementation coroutinesDep

    // See https://github.com/NovaCrypto/BIP39/issues/43
    implementation("com.github.NovaCrypto:BIP39:0e7fa95f80") {
        exclude group: "io.github.novacrypto", module: "ToRuntime"
        exclude group: "io.github.novacrypto", module: "SHA256"
    }
    implementation("com.github.NovaCrypto:Sha256:57bed72da5") {
        exclude group: "io.github.novacrypto", module: "ToRuntime"
    }
    implementation "com.github.NovaCrypto:ToRuntime:c3ae3080eb"

    testImplementation jUnitDep
    testImplementation mockitoDep
    testImplementation project(':substrate-sdk-android')

    androidTestImplementation androidTestRunnerDep
    androidTestImplementation androidTestRulesDep
    androidTestImplementation androidJunitDep
    androidTestImplementation project(':substrate-sdk-android')

    testImplementation coroutinesTestDep
}

def computeVersionName() {
    def verMatcher = rootProject.versionName =~ /^([0-9]+\.[0-9]+)\.([0-9]+)$/
    if (!verMatcher.find()) {
        throw new GradleException("Invalid versionName!")
    }
    if (System.getenv('BUILD_ID') != null && System.getenv('BRANCH_NAME') != null && System.getenv('BRANCH_NAME') == 'develop') {
        return "${verMatcher.group(1)}.${System.getenv('BUILD_ID')}"
    } else {
        return "${rootProject.versionName}"
    }
}
